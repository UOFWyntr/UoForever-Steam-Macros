//----------------------------------------------------------------//
//Auto Peace Tame V1 by Wyntr Â©2017
//----------------------------------------------------------------//
//Features:
//It can tell if a tame is attacking and will peace it.
//Auto walks after tame during taming.
//Range of script is 12 tiles.
//Targets closest first.
//Will repeace on failure after 5 seconds.
//Tame rename list for easy changing.
//Updates:
//V1
//Added tame war check.
//Tweaked taming routine to accomodate renames correctly.
//Revamped targeting routine.
//Added tamename list.
//Special Instructions:
//--------Peace Timer----------//
//The peacetimer can be set below inside of lists. It is how long the script will tame for before using peacemaking again on the mob. Default is 30 seconds. For harder tames a shorter time may be required.
//Peacetimer
//if not listexists peacetimer
//  createlist peacetimer
//  pushlist peacetimer '30000' <--edit here
//endif
//--------Tame Names------//
//To change the name of your tames rename, change inside the quotes where it say 'BBQ' down below inside of lists.
//if not listexists tamename
//  createlist tamename
//  pushlist tamename 'BBQ' <--edit name here
//endif
//--------Peaceables List------//
//The total number of tames in the pushlist section for peaceables, default is 44, has to be the number in the for loop directly below the pushlist.
//--------------------------------Setup--------------------------------//
@clearjournal
@cancelautotarget
@canceltarget
@unsetalias peacetame
//--------------------------------Lists--------------------------------//
//Peacetimer
if not listexists peacetimer
  createlist peacetimer
  pushlist peacetimer '30000'
endif
//TameName
if not listexists tamename
  createlist tamename
  pushlist tamename 'BBQ'
endif
//Tameables List
if not listexists peaceables
  createlist peaceables
endif
//targeting distance
@clearlist distance
if not listexists distance
  @createlist distance
endif
pushlist distance 1
pushlist distance 2
pushlist distance 3
pushlist distance 4
pushlist distance 5
pushlist distance 6
pushlist distance 7
pushlist distance 8
pushlist distance 9
pushlist distance 10
pushlist distance 11
pushlist distance 12
//-------------------------------Instrument--------------------------------//
//Find Instrument
if not @findobject 'instrument'
  if @findtype '0xe9e' 'backpack' 'any' //Tambourine with red tassle
    @setalias 'instrument' 'found'
  elseif @findtype '0x2805' 'any' 'backpack' //Flute
    @setalias 'instrument' 'found'
    headmsg "Instrument set." '66'
    @useobject 'instrument'
  elseif @findtype '0xe9d' '0' 'backpack'//Tambourine
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xe9c' '0' 'backpack' //Drum
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xeb3' '0' 'backpack' //Lute
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xeb2' '0' 'backpack' //Lap Harp
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xeb1' '0' 'backpack' //Standing Harp
    @setalias 'instrument' 'found'
    useobject 'instrument'
  else
    headmsg 'No instrument found'
    stop
  endif
endif
//--------------------------------Tame Search--------------------------------//
//find tames from list
if not @findobject peacetame
  for 0 to distance
    pushlist 'peaceables' 0x7 //Hellcat (Large)
    pushlist 'peaceables' 0x62 //Hellhound
    pushlist 'peaceables' 0xbc //Savage Ridgeback
    pushlist 'peaceables' 0xbb //Ridgeback
    pushlist 'peaceables' 0x17 //Dire Wolf
    pushlist 'peaceables' 0xce //Lava Lizard
    pushlist 'peaceables' 0x14 //Frost spider
    pushlist 'peaceables' 0xc9 //Hellcat (Small)
    pushlist 'peaceables' 0xda //Frenzied Ostard
    pushlist 'peaceables' 0x50 //Giant Toad
    pushlist 'peaceables' 0x22 //White Wolf
    pushlist 'peaceables' 0x25 //White Wolf2
    pushlist 'peaceables' 0x1c //Giant Spider
    pushlist 'peaceables' 0x41 //Snow leopard
    pushlist 'peaceables' 0x40 //Snow leopard
    pushlist 'peaceables' 0x19 //Gray wolf
    pushlist 'peaceables' 0x1b //Gray wolf2
    pushlist 'peaceables' 0x30 //Scorpion
    pushlist 'peaceables' 0xca //Alligator
    pushlist 'peaceables' 0x3f //Cougar
    pushlist 'peaceables' 0xd6 //Panther
    pushlist 'peaceables' 0xa7 //Brown Bear
    pushlist 'peaceables' 0xd3 //Black Bear
    pushlist 'peaceables' 0xdc //Llama
    pushlist 'peaceables' 0x124 //Pack Llama
    pushlist 'peaceables' 0x123 //Pack Horse
    pushlist 'peaceables' 0xc8 //Horse
    pushlist 'peaceables' 0xe2 //Horse2
    pushlist 'peaceables' 0xcc //Horse3
    pushlist 'peaceables' 0xe4 //Horse4
    pushlist 'peaceables' 0xed //Giant Rat
    pushlist 'peaceables' 0xdb //Forest Ostard
    pushlist 'peaceables' 0xd2 //Desert Ostard
    pushlist 'peaceables' 0x122 //Boar
    pushlist 'peaceables' 0xe1 //Timber Wolf
    pushlist 'peaceables' 0xed //Hind
    pushlist 'peaceables' 0x51 //Bullfrog
    pushlist 'peaceables' 0x1d //gorilla
    pushlist 'peaceables' 0xdd //Walrus
    pushlist 'peaceables' 0xd5 //Polar Bear
    pushlist 'peaceables' 0xd4 //Grizzly Bear
    pushlist 'peaceables' 0xe8 //Bull
    pushlist 'peaceables' 0xe9 //Bull2
    pushlist 'peaceables' 0xea //Great Hart
    for 44
      if @findtype peaceables[0] any ground 1 distance[]
        @setalias peacetame found
        headmsg '**Tame Found**' 156 peacetame
        break
      endif
      poplist peaceables front
    endfor
    if @findobject peacetame
      break
    endif
  endfor
endif
//no tames msg
if not @findobject peacetame
  headmsg '**No Tames**' 32
  stop
endif
//----------------------------Peace Routine-----------------------------//
//Peace if needed.
if war peacetame
  if @inrange peacetame 10
    while not @injournal 'play hypnotic music' system
      headmsg '**Peacing..**' 56 peacetame
      useskill peacemaking
      pause 500
      //instrument check
      if @injournal 'instrument shall you' system
        pause 500
        target! instrument
        clearjournal
        pause 500
      endif
      //skill resuse timer
      if @injournal 'must wait a few moments to use' system
        headmsg '**Peace Not Ready**' 32
        @canceltarget
        clearjournal
        replay
      endif
      //target unseen check
      if @injournal 'cannot be seen' system
        headmsg '**Target Cannot Be Seen**' 32
        @canceltarget
        clearjournal
        stop
      endif
      //target tame for peace
      waitfortarget 5000
      target! peacetame
      pause 500
      //failure check
      if @injournal 'play poorly, and there is no' system
        headmsg '**PEACE FAILURE**' 32 peacetame
        sysmsg 'Retrying Peace in 5 seconds..' 32
        pause 5000
        clearjournal
        replay
      endif
      //final check for peace success
      if @injournal 'play hypnotic music' system or @injournal 'creature is already being' system
        if @injournal 'creature is already being' system
          headmsg '**Already Peaced**' 32 peacetame
          warmode on
          warmode off
          clearjournal
          break
        else
          headmsg '!!Peace Succesful!!' 56 peacetame
          createtimer peacetimer
          clearjournal
          pause 2500
          warmode on
          warmode off
          pause 2500
          break
        endif
      endif
    endwhile
  endif
endif
//----------------------------Taming Routine---------------------------//
headmsg '**Tame Me**' 81 peacetame
while @timer peacetimer < peacetimer[0] or not war peacetame
  if @inrange peacetame 2
    useskill 'animal taming'
    waitfortarget 1000
    target! peacetame
    //range check and movement
    if not @inrange 'peacetame' 1
      if @x 'peacetame' > x 'self' and @y 'peacetame' > y 'self'
        walk 'Southeast'
      elseif @x 'peacetame' < x 'self' and @y 'peacetame' > y 'self'
        walk 'Southwest'
      elseif @x 'peacetame' > x 'self' and @y 'peacetame' < y 'self'
        walk 'Northeast'
      elseif @x 'peacetame' < x 'self' and @y 'peacetame' < y 'self'
        walk 'Northwest'
      elseif @x 'peacetame' > x 'self' and @y 'peacetame' == y 'self'
        walk 'East'
      elseif @x 'peacetame' < x 'self' and @y 'peacetame' == y 'self'
        walk 'West'
      elseif @x 'peacetame' == x 'self' and @y 'peacetame' > y 'self'
        walk 'South'
      elseif @x 'peacetame' == x 'self' and @y 'peacetame' < y 'self'
        walk 'North'
      endif
    endif
    //------------------------------Rename--------------------------------//
    //rename
    if name peacetame != tamename[0]
      rename peacetame tamename[0]
      pause 300
      if @injournal 'must wait a while longer to rename' system
        pause 1500
        clearjournal
      endif
    endif
    //release
    if name peacetame == tamename[0]
      waitforcontext peacetame 8 1000
      waitforgump 0x909cc741 1000
      //release gump
      if @replygump 0x909cc741 2
        headmsg '**Released**' 56 peacetame
        ignoreobject peacetame
        @unsetalias peacetame
        pause 2000
        shownames mobiles
        // here is where you can loop the script of stop it
        //replay
        stop
      endif
    endif
  else
    headmsg '**Get Closer**' 56 peacetame
    while not @inrange peacetame 2
    endwhile
  endif
endwhile
replay
