//Dual Pet Heal V4
//There are TWO locations in this script that you can adjust for emergency greater heals. Look for the emergency heal sections and adjust the hits. It will look like this:
//-------Emergency Heal-----//
//if hits meta < 25 //lower limit, this is a percentage, set it for when to START emergency healing
//  headmsg 'META LOW HITS' 29 meta
//  while hits meta < 45 //upper limit, this is a percentage, set it for when to STOP emergency healing
//-------------------------------------------------
//Setup
//-------------------------------------------------
clearjournal
while not @findobject meta
  headmsg 'Select meta..' 43
  promptalias meta
endwhile
while not @findobject pet
  headmsg 'Select secondary pet..' 43
  promptalias pet
endwhile
//-------Bandaid Timer-----//
//Bandaid timer check for restarting macro while healing
if @timer metabandaid < 5200
  headmsg '**Already healing**' 55
  while @timer metabandaid < 5200
  endwhile
endif
//-------------------------------------------------
//Main Statement
//-------------------------------------------------
while hits meta < maxhits or hits pet < maxhits
  //-------Heal Meta First-----//
  sysmsg 'Checking meta health..' 43
  while diffhits meta >= diffhits pet
    if inrange meta 1
      //-------Res Check-----//
      if hits meta < 1
        if hits meta == 0
          while hits meta == 0
            if @injournal 'are able to resurrect' system
              clearjournal
              replay
            endif
            sysmsg 'Attempting to res meta..' 43
            bandage meta
            while not @injournal 'fail to resurrect' system
              if @gumpexists 0x4da72c0
                headmsg 'Ressing' 56 meta
                replygump 0x4da72c0 1
                pause 500
                replay
              endif
              if hits meta > 0
                replay
              endif
            endwhile
            clearjournal
            pause 250
          endwhile
        endif
      endif
      //-------END Res Check-----//
      //-------Cure Meta-----//
      if poisoned meta
        sysmsg 'Curing meta..' 43
        bandage meta
        createtimer metabandaid
        while not @injournal 'finish applying the' system
          if not inrange meta 2
            headmsg 'Out of range' 43 meta
            removetimer metabandaid
            clearjournal
            stop
          endif
          pause 500
        endwhile
        removetimer metabandaid
        clearjournal
        //-------Emergency Cure-----//
        if poisoned meta
          cast cure
          waitfortarget 5000
          target! meta
        endif
      endif
      //-------Emergency Heal-----//
      if hits meta < 25
        headmsg 'META LOW HITS' 29 meta
        while hits meta < 45
          while mana < 11
            useskill meditation
            while not mana > 16
            endwhile
          endwhile
          if poisoned meta
            cast cure
            waitfortarget 5000
            target! meta
            pause 200
          endif
          cast 'greater heal'
          waitfortarget 5000
          target! meta
          pause 1000
        endwhile
      endif
      //-------Heal Meta-----//
      if hits meta < maxhits
        headmsg 'Healing meta..' 43 meta
        if poisoned meta
          cast cure
          waitfortarget 5000
          target! meta
          pause 200
        endif
        bandage meta
        createtimer metabandaid
        while not @injournal 'finish applying the' system
          if not inrange meta 2
            headmsg 'Out of range' 43 meta
            removetimer metabandaid
            clearjournal
            stop
          endif
          pause 500
        endwhile
        removetimer metabandaid
        clearjournal
      else
        headmsg '**Meta Full Health**' 85 meta
        replay
      endif
    else
      headmsg 'Get closer!' 48 meta
      stop
    endif
  endwhile
  //-------Heal Secondary Pet First-----//
  sysmsg 'Checking secondary pet health..' 43
  while diffhits pet >= diffhits meta
    if inrange pet 1
      //-------Res Check-----//
      if hits pet < 1
        if hits pet == 0
          while hits pet == 0
            if @injournal 'are able to resurrect' system
              clearjournal
              replay
            endif
            sysmsg 'Attempting to res secondary pet..' 43
            bandage pet
            while not @injournal 'fail to resurrect' system
              if @gumpexists 0x4da72c0
                headmsg 'Ressing' 56 pet
                replygump 0x4da72c0 1
                pause 500
                replay
              endif
              if hits pet > 0
                replay
              endif
            endwhile
            clearjournal
            pause 250
          endwhile
        endif
      endif
      //-------END Res Check-----//
      //-------Cure Secondary Pet-----//
      if poisoned pet
        sysmsg 'Curing secondary pet..' 43
        bandage pet
        createtimer metabandaid
        while not @injournal 'finish applying the' system
          if not inrange pet 2
            headmsg 'Out of range' 43 pet
            removetimer metabandaid
            clearjournal
            stop
          endif
          pause 500
        endwhile
        removetimer metabandaid
        clearjournal
        //-------Emergency Cure-----//
        if poisoned pet
          cast cure
          waitfortarget 5000
          target! pet
        endif
      endif
      //-------Emergency Heal-----//
      if hits pet < 25
        headmsg '2ND PET LOW HEALTH' 29 pet
        while hits pet < 45
          while mana < 11
            useskill meditation
            while not mana > 16
            endwhile
          endwhile
          if poisoned pet
            cast cure
            waitfortarget 5000
            target! pet
            pause 200
          endif
          cast 'greater heal'
          waitfortarget 5000
          target! pet
          pause 1000
        endwhile
      endif
      //-------Heal Secondary Pet-----//
      if hits pet < maxhits
        headmsg 'Healing secondary pet..' 43 pet
        if poisoned pet
          cast cure
          waitfortarget 5000
          target! pet
          pause 200
        endif
        bandage pet
        createtimer metabandaid
        while not @injournal 'finish applying the' system
          if not inrange pet 2
            headmsg 'Out of range' 43 pet
            removetimer metabandaid
            clearjournal
            stop
          endif
          pause 500
        endwhile
        removetimer metabandaid
        clearjournal
      else
        headmsg '**2nd Pet Full Health**' 85 pet
        replay
      endif
    else
      headmsg 'Get closer!' 48 pet
      stop
    endif
  endwhile
endwhile
//END
headmsg '**Pets At Full Health**' 85
