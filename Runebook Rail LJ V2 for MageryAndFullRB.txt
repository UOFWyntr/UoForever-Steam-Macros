//---------------------------------------------------------
//Config
//---------------------------------------------------------
clearjournal
//find wood runebook
if not @findobject 'lumberrunebook'
  sysmsg 'Select Rune Book for lumberjacking..' 73
  promptalias 'lumberrunebook'
endif
//homerunebook make sure and set the rune for home in this book to default
while not @findobject homerunebook
  headmsg 'Select home runebook'
  promptalias homerunebook
endwhile
//---------------------------------------------------------
//Lists
//---------------------------------------------------------
// !! Find and equip any axe in backpack ***
if not listexists 'AxeList'
  createlist 'AxeList'
endif
// !! axe data
@clearlist 'AxeList'
@pushlist 'AxeList' 0xf43 //Hatchet
@pushlist 'AxeList' 0xf45 //Executioner's Axe
@pushlist 'AxeList' 0xf4b  //Double Axe
@pushlist 'AxeList' 0x13fb //Large Battle Axe
@pushlist 'AxeList' 0x1443 //Two Handed Axe
@pushlist 'AxeList' 0xf47  //Battle Axe
@pushlist 'AxeList' 0xf49  //Axe
//
for 0 to 'AxeList'
  if @findtype 'AxeList[]' 'any' 'backpack'
    unsetalias 'AXE'
    setalias 'AXE' 'found'
  endif
endfor
//monsters
if not listexists monster
  @createlist monsters
endif
if list monsters == 0
  pushlist monsters 0x1 //ogre
  pushlist monsters 0x11 //orc without weapon
  pushlist monsters 0x12 //Ettin
  pushlist monsters 0x15 //LJ Serpent
  pushlist monsters 0x2a //ratman
  pushlist monsters 0x36 //troll no weapon
  //pushlist monsters 0x //
endif
//context menus for book
if not listexists 'recalllumber'
  @createlist 'recalllumber'
endif
poplist 'recalllumber' 'front'
if 'list' 'recalllumber' == '0'
  pushlist 'recalllumber' 5
  pushlist 'recalllumber' 11
  pushlist 'recalllumber' 17
  pushlist 'recalllumber' 23
  //
  pushlist 'recalllumber' 29
  pushlist 'recalllumber' 35
  pushlist 'recalllumber' 41
  pushlist 'recalllumber' 47
  //
  pushlist 'recalllumber' 53
  pushlist 'recalllumber' 59
  pushlist 'recalllumber' 65
  pushlist 'recalllumber' 71
  //
  pushlist 'recalllumber' 77
  pushlist 'recalllumber' 83
  pushlist 'recalllumber' 89
  pushlist 'recalllumber' 95
  //
endif
//callout for positions
if not listexists 'recallnumber'
  @createlist 'recallnumber'
endif
poplist 'recallnumber' 'front'
if 'list' 'recallnumber' == '0'
  @pushlist 'recallnumber' 'Rune 1'
  @pushlist 'recallnumber' 'Rune 2'
  @pushlist 'recallnumber' 'Rune 3'
  @pushlist 'recallnumber' 'Rune 4'
  //
  @pushlist 'recallnumber' 'Rune 5'
  @pushlist 'recallnumber' 'Rune 6'
  @pushlist 'recallnumber' 'Rune 7'
  @pushlist 'recallnumber' 'Rune 8'
  //
  @pushlist 'recallnumber' 'Rune 9'
  @pushlist 'recallnumber' 'Rune 10'
  @pushlist 'recallnumber' 'Rune 11'
  @pushlist 'recallnumber' 'Rune 12'
  //
  @pushlist 'recallnumber' 'Rune 13'
  @pushlist 'recallnumber' 'Rune 14'
  @pushlist 'recallnumber' 'Rune 15'
  @pushlist 'recallnumber' 'Rune 16'
  //
endif
//---------------------------------------------------------
//Recall To Chop
//---------------------------------------------------------
//mana check
while not mana > 11
  headmsg 'Waiting for mana'
  useskill meditation
  pause 5000
endwhile
//recall
headmsg 'Recall to...' 26
headmsg recallnumber[0] 26
headmsg recalllumber[0] 26
pause 1000
useobject 'lumberrunebook'
waitforgump 0x554b87f3 5000
replygump '0x554b87f3' recalllumber[0]
pause 3000
//---------------------------------------------------------
//Rail Chopping
//---------------------------------------------------------
// !! Equip Axe
if not @findlayer 'self' 2
  equipitem 'AXE' 2
  pause 600
endif
//set timer
if not timerexists timetochop
  createtimer timetochop
endif
settimer timetochop 0
//*******************************************************
// !! Main chopping routine
//*******************************************************
// This is where you change the wait time of not recieving any wood to your backpack
//If the timer reaches 10 seconds it will recall you to next rune
while timer timetochop < 15000
  canceltarget
  useobject 'LeftHand'
  waitfortarget 2000
  targettileoffset! 1 0 0
  pause 500
  //////////////
  if @injournal "You can't" system
    sysmsg 'No Tree!' 289
    walk south
    pause 100
    clearjournal
  endif
  if @injournal 'not enough wood' system
    sysmsg 'no more wood' 23
    walk south
    pause 100
    clearjournal
  endif
  //wood check
  if @injournal 'some ordinary' 'system'
    headmsg '--Ordinary wood--' '1150'
    clearjournal
    settimer timetochop 0
  endif
  if @injournal 'some oak' 'system'
    headmsg '--OAK--' '2010'
    clearjournal
    settimer timetochop 0
  endif
  if @injournal 'some ash' 'system'
    headmsg '--ASH--' '1191'
    clearjournal
    settimer timetochop 0
  endif
  if @injournal 'some yew' 'system'
    headmsg '--YEW--' '1192'
    msg '[e ah'
    clearjournal
    settimer timetochop 0
  endif
  if @injournal 'some heartwood' 'system'
    headmsg '>!!!--HEARTWOOD--!!!<' '1193'
    msg '[e yea'
    clearjournal
    settimer timetochop 0
  endif
  if @injournal 'some bloodwood' 'system'
    headmsg '>!!!--BLOODWOOD--!!!<' '1194'
    msg '[e woohoo'
    clearjournal
    settimer timetochop 0
  endif
  if @injournal 'some frostwood' 'system'
    headmsg '>!!!--FROSTWOOD--!!!<' '1151'
    msg '[e woohoo'
    clearjournal
    settimer timetochop 0
  endif
  //----------------------------------------------------
  //Monster check
  //----------------------------------------------------
  if not @findobject targ any ground 1 10
    for 0 to monsters
      if @findtype monsters[] any ground 1 10
        sysmsg 'Monster found' 89
        headmsg 'Target Set' 89
        @setalias targ found
        break
      endif
    endfor
  endif
  //----------------------------------------------------
  //Monster attack
  //----------------------------------------------------
  if @findobject targ any world 1 2
    headmsg 'Attacking' 89 targ
    attack targ
    while @findobject targ any world 1 2
      if poisoned self or hits < 75
        clearhands both
        pause 500
        if poisoned self
          while poisoned self
            cast cure self
            pause 1000
          endwhile
        endif
        //mini emergency heal
        if hits < 35
          while hits < 50
            if poisoned self
              while poisoned self
                cast cure self
                pause 1000
              endwhile
            endif
            cast 'heal' self
            pause 1000
          endwhile
        endif
        if hits < 75
          while hits < 75
            if poisoned self
              while poisoned self
                cast cure self
                pause 1000
              endwhile
            endif
            cast 'greater heal' self
            pause 1000
          endwhile
        endif
      endif
      if not @findlayer 'self' 2
        equipitem 'AXE' 2
        pause 600
      endif
    endwhile
    settimer timetochop 0
  endif
  //---------------------------------------------------------
  //Weight Check
  //---------------------------------------------------------
  if diffweight < 30
    sysmsg 'Backpack Full..' 23
    while not mana > 11
      headmsg 'Waiting for mana'
      useskill meditation
      pause 5000
    endwhile
    pause 500
    headmsg 'Recalling home..' 23
    useobject homerunebook
    waitforgump 0x554b87f3 5000
    replygump 0x554b87f3 2
    pause 5000
    while not @findobject woodchest
      headmsg 'Select wood chest..' 56
      promptalias woodchest
      pause 1000
    endwhile
    useobject woodchest
    pause 1000
    sysmsg 'Moving wood..' 23
    while @findtype 0x1bdd any backpack
      moveitem found woodchest
      pause 1000
    endwhile
    pause 500
    //-------------------Move Reagents-----------------------//
    //Blackpearl
    if @counttype 0xf7a any backpack < 5
      if movetype 0xf7a woodchest backpack 0 0 0 any 20
        pause 1000
      else
        headmsg 'Out of Black Pearl..' 29
        stop
      endif
    endif
    //Bloodmoss
    if @counttype 0xf7b any backpack < 5
      if movetype 0xf7b woodchest backpack 0 0 0 any 20
        pause 1000
      else
        headmsg 'Out of Bloodmoss..' 29
        stop
      endif
    endif
    //Mandrake Root
    if @counttype 0xf86 any backpack < 5
      if movetype 0xf86 woodchest backpack 0 0 0 any 20
        pause 1000
      else
        headmsg 'Out of Mandrake Root..' 29
        stop
      endif
    endif
    sysmsg 'Next Rune' 100
    replay
  endif
  //chopping loop delay
  pause 100
endwhile
//*******************************************************
// !! Main chopping routine END
//*******************************************************
replay
