//----------------------------------------------------
//Config
//----------------------------------------------------
while not @findobject 'bodrunebook' any backpack
  headmsg 'Select Bod Runebook'
  promptalias 'bodrunebook'
endwhile
while not @findobject 'tailorbodbook' any backpack
  headmsg 'Select Tailor Bod Book'
  promptalias 'tailorbodbook'
endwhile
while not @findobject 'smithbodbook' any backpack
  headmsg 'Select Smith Bod Book'
  promptalias 'smithbodbook'
endwhile
//----------------------------------------------------
//Loose bod cleanup
//----------------------------------------------------
//tailor bods
while @findtype 0x14ef 1155 backpack
  @moveitem found tailorbodbook
  pause 1000
endwhile
//smith bods
while @findtype 0x14ef 1102 backpack
  @moveitem found smithbodbook
  pause 1000
endwhile
//----------------------------------------------------
//Timer check
//----------------------------------------------------
if timerexists bods
  //stop here while timer is less than 5 hours 4 mins
  while timer bods < 18240000
    headmsg 'Bod Timer Active' 58
    @timermsg bods 32
    pause 5000
  endwhile
  //once time is up it clears the timer and grabs more bods
  if timer bods > 18240000
    headmsg 'Bod timer is up! Time to get Bods' 32
    removetimer bods
  endif
endif
//----------------------------------------------------
//Recall to tailor
//----------------------------------------------------
@useobject 'bodrunebook'
waitforgump 0x554b87f3 5000
replygump 0x554b87f3 5
pause 4000
//----------------------------------------------------
//Search for tailor
//----------------------------------------------------
while not @findobject tailor
  headmsg 'Select tailor'
  promptalias tailor
  pause 2000
endwhile
//----------------------------------------------------
//Confirm bod is available
//----------------------------------------------------
if @findobject tailor
  headmsg 'Tailor found'
  headmsg 'Checking if bod is available'
  waitforcontext tailor 1 5000
  pause 2000
  while not @injournal 'can get an order' system
    headmsg 'Bod Unavailable'
    pause 60000
    waitforcontext tailor 1 5000
    pause 2000
    if @injournal 'can get an order' system
      headmsg 'You can get a bod now'
      break
    endif
  endwhile
  headmsg 'Bod Available'
else
  headmsg 'Tailor not found'
  stop
endif
//----------------------------------------------------
//Sell to tailor
//----------------------------------------------------
if @findobject tailor
  clearjournal
  while not @findtype 0x14ef 1155 backpack
    if @findtype 0xf9d any backpack
      //msg 'taffy sell'
      waitforcontext tailor 3 15000
      pause 1000
      clickscreen 229 103 double left
      pause 1000
      clickscreen 315 445 single left
      pause 3000
    else
      //msg 'taffy buy'
      waitforcontext tailor 2 15000
      pause 1000
    endif
    headmsg 'Check for bod gump' 48
    pause 2000
    if @gumpexists 0x9bade6ea
      headmsg 'Sbod gump detected'
      headmsg 'Accepting Bod'
      replygump 0x9bade6ea 1
      pause 3000
    endif
    pause 1000
    if @gumpexists 0xbe0dad1e
      headmsg 'Lbod gump detected'
      headmsg 'Accepting Bod'
      replygump 0xbe0dad1e 1
      pause 3000
    endif
    pause 1000
  endwhile
  //bod gump
endif
headmsg 'Moving tailor bod to book'
while @findtype 0x14ef 1155 backpack
  @moveitem found tailorbodbook
  pause 1000
endwhile
waitforgump 0x54f555df 5000
replygump 0x54f555df 0
pause 1000
headmsg 'Tailor Bod Done' 69
//----------------------------------------------------
//Recall to smith
//----------------------------------------------------
@useobject 'bodrunebook'
waitforgump 0x554b87f3 5000
replygump 0x554b87f3 11
pause 4000
//----------------------------------------------------
//Search for smith
//----------------------------------------------------
while not @findobject blacksmith
  headmsg 'Select blacksmith'
  promptalias blacksmith
  pause 2000
endwhile
//----------------------------------------------------
//Confirm bod is available
//----------------------------------------------------
if @findobject blacksmith
  headmsg 'Smith found'
  headmsg 'Checking if bod is available'
  waitforcontext blacksmith 1 5000
  pause 2000
  while not @injournal 'can get an order' system
    headmsg 'Bod Unavailable'
    pause 60000
    waitforcontext blacksmith 1 5000
    pause 2000
    if @injournal 'can get an order' system
      headmsg 'You can get a bod now'
      break
    endif
  endwhile
  headmsg 'Bod Available'
else
  headmsg 'Blacksmith not found'
  stop
endif
//----------------------------------------------------
//Sell to smith
//----------------------------------------------------
if @findobject blacksmith
  clearjournal
  while not @findtype 0x14ef 1102 backpack
    if @findtype 0x1bf2 any backpack
      //msg 'aldrick sell'
      waitforcontext blacksmith 3 15000
      pause 1000
      clickscreen 229 103 double left
      pause 1000
      clickscreen 315 445 single left
      pause 3000
    else
      //msg 'aldrick buy'
      waitforcontext blacksmith 2 15000
      pause 1000
    endif
    headmsg 'Check for bod gump' 48
    pause 2000
    if @gumpexists 0x9bade6ea
      headmsg 'Sbod gump detected'
      headmsg 'Accepting Bod'
      replygump 0x9bade6ea 1
      pause 3000
    endif
    pause 1000
    if @gumpexists 0xbe0dad1e
      headmsg 'Lbod gump detected'
      headmsg 'Accepting Bod'
      replygump 0xbe0dad1e 1
      pause 3000
    endif
    pause 1000
  endwhile
endif
headmsg 'Moving smith bod to book'
while @findtype 0x14ef 1102 backpack
  @moveitem found smithbodbook
  pause 1000
endwhile
waitforgump 0x54f555df 5000
replygump 0x54f555df 0
pause 1000
headmsg 'Blacksmith Bod Done' 55
//----------------------------------------------------
//Recall to brit bank door
//----------------------------------------------------
@useobject 'bodrunebook'
waitforgump 0x554b87f3 5000
replygump 0x554b87f3 17
pause 4000
turn south
turn south
turn north
walk north
walk north
walk north
//----------------------------------------------------
//Create 5 hour timer
//----------------------------------------------------
if not timerexists bods
  @createtimer bods
endif
pause 500
//hangs here until timer reachers 5 hours 4 mins
while not timer bods > 18240000
  headmsg 'Total time waiting for bod:' 21
  @timermsg bods 32
  pause 2000
endwhile
pause 1000
headmsg 'BOD TIME!'
playsound f_hey.wav
pause 1000
replay
