//clearignorelist
clearjournal
cancelautotarget
canceltarget
if not listexists peaceables
  createlist peaceables
  //
  pushlist peaceables 0xcd //bunny
  pushlist peaceables 0xee //rat
  pushlist peaceables 0xc9 //cat
  pushlist peaceables 0xcf //sheep
  //
  pushlist peaceables 0x7 //large hellcats
  pushlist peaceables 0x62 //hell hounds
  pushlist peaceables 0xce //lava lizards
  pushlist peaceables 0x30 //scorpion
endif
//peacetimer
if not listexists peacetimer
  createlist peacetimer
  pushlist peacetimer '30000'
endif
//
@clearlist distance
if not listexists distance
  @createlist distance
endif
pushlist distance 1
pushlist distance 2
pushlist distance 3
pushlist distance 4
pushlist distance 5
pushlist distance 6
pushlist distance 7
pushlist distance 8
pushlist distance 9
pushlist distance 10
pushlist distance 11
pushlist distance 12
//Find Instrument
if not @findobject 'instrument'
  if @findtype '0xe9e' 'backpack' 'any' //Tambourine with red tassle
    @setalias 'instrument' 'found'
  elseif @findtype '0x2805' 'any' 'backpack' //Flute
    @setalias 'instrument' 'found'
    headmsg "Instrument set." '66'
    @useobject 'instrument'
  elseif @findtype '0xe9d' '0' 'backpack'//Tambourine
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xe9c' '0' 'backpack' //Drum
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xeb3' '0' 'backpack' //Lute
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xeb2' '0' 'backpack' //Lap Harp
    @setalias 'instrument' 'found'
    @useobject 'instrument'
  elseif @findtype '0xeb1' '0' 'backpack' //Standing Harp
    @setalias 'instrument' 'found'
    useobject 'instrument'
  else
    headmsg 'No instrument found'
    stop
  endif
endif
//find tames from list
if not @findobject peacetame
  for 0 to distance
    for 0 to peaceables
      if @findtype peaceables[] any ground 1 distance[]
        @setalias peacetame found
        headmsg '**Tame Found**' 156 peacetame
        replay
      endif
    endfor
  endfor
endif
if not @findobject peacetame
  headmsg '**No Tames**' 32
  stop
endif
//
if war peacetame
  if @inrange peacetame 10
    while not @injournal 'play hypnotic music' system
      headmsg '**Peacing..**' 56 peacetame
      useskill peacemaking
      pause 500
      if @injournal 'instrument shall you' system
        pause 500
        target! instrument
        clearjournal
        pause 500
      endif
      if @injournal 'must wait a few moments to use' system
        headmsg '**Peace Not Ready**' 32
        @canceltarget
        clearjournal
        replay
      endif
      if @injournal 'cannot be seen' system
        headmsg '**Target Cannot Be Seen**' 32
        @canceltarget
        clearjournal
        stop
      endif
      waitfortarget 5000
      target! peacetame
      pause 500
      if @injournal 'play poorly, and there is no' system
        headmsg '**PEACE FAILURE**' 32 peacetame
        pause 5000
        clearjournal
        replay
      endif
      if @injournal 'play hypnotic music' system or @injournal 'creature is already being' system
        if @injournal 'creature is already being' system
          headmsg '**Already Peaced**' 32 peacetame
          clearjournal
          break
        else
          headmsg '!!Peace Succesful!!' 56 peacetame
          createtimer peacetimer
          clearjournal
          pause 2500
          warmode on
          warmode off
          pause 2500
          break
        endif
      endif
    endwhile
  endif
endif
headmsg '**Tame Me**' 81 peacetame
while @timer peacetimer < peacetimer[0] or not war peacetame
  if @inrange peacetame 2
    useskill 'animal taming'
    waitfortarget 1000
    target! peacetame
    if not @inrange 'peacetame' 1
      if @x 'peacetame' > x 'self' and @y 'peacetame' > y 'self'
        walk 'Southeast'
      elseif @x 'peacetame' < x 'self' and @y 'peacetame' > y 'self'
        walk 'Southwest'
      elseif @x 'peacetame' > x 'self' and @y 'peacetame' < y 'self'
        walk 'Northeast'
      elseif @x 'peacetame' < x 'self' and @y 'peacetame' < y 'self'
        walk 'Northwest'
      elseif @x 'peacetame' > x 'self' and @y 'peacetame' == y 'self'
        walk 'East'
      elseif @x 'peacetame' < x 'self' and @y 'peacetame' == y 'self'
        walk 'West'
      elseif @x 'peacetame' == x 'self' and @y 'peacetame' > y 'self'
        walk 'South'
      elseif @x 'peacetame' == x 'self' and @y 'peacetame' < y 'self'
        walk 'North'
      endif
    endif
    //rename
    if name peacetame != 'BBQ'
      rename peacetame 'BBQ'
      pause 300
      if @injournal 'must wait a while longer to rename' system
        pause 2500
        clearjournal
      endif
    endif
    //release
    if name peacetame == 'BBQ'
      waitforcontext peacetame 8 1000
      //pause 500
      waitforgump 0x909cc741 1000
      //pause 500
      if @replygump 0x909cc741 2
        headmsg '**Released**' 56 peacetame
        ignoreobject peacetame
        @unsetalias peacetame
        stop
      endif
    endif
  else
    headmsg '**Get Closer**' 56
    while not @inrange peacetame 2
    endwhile
  endif
endwhile
replay
